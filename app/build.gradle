apply plugin: 'com.android.application'

android {
    compileSdk 34
    buildToolsVersion '34.0.0'
    defaultConfig {
        applicationId "co.epitre.aelf_lectures"
        minSdkVersion 25
        targetSdkVersion 34
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
    buildTypes {
        integration {
            // Release build on test device
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.txt'
            signingConfig signingConfigs.debug
        }
        release {
            // Release build for the Play Store
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.txt'
            ndk {
                debugSymbolLevel 'SYMBOL_TABLE'
            }
        }
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'co.epitre.aelf_lectures'
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.1'

    // Material design
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'com.google.android.material:material:1.9.0'

    // HTTP layer (3.12.X is the last version to support Android 4.x)
    implementation 'com.squareup.okhttp3:okhttp:3.12.13'
    implementation 'com.squareup.moshi:moshi:1.9.2'

    // Foundation
    implementation 'org.greenrobot:eventbus:3.1.1'

    // Sqlite
    implementation project(path: ':sqlite3')
}

task downloadMarkJs(type: DownloadFile) {
    sourceUrl = 'https://raw.githubusercontent.com/julmot/mark.js/8.11.1/dist/mark.min.js'
    target = new File('./src/main/assets/js/mark.8.11.1.min.js')
}
preBuild.dependsOn downloadMarkJs

//
// Custom tasks
//

class DownloadFile extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}
