apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdk 34
    buildToolsVersion '34.0.0'
    defaultConfig {
        applicationId "co.epitre.aelf_lectures"
        minSdkVersion 25
        targetSdkVersion 34
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }
    }
    buildTypes {
        integration {
            // Release build on test device
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-rules-integration.pro'
            signingConfig signingConfigs.debug
        }
        release {
            // Release build for the Play Store
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'SYMBOL_TABLE'
            }
        }
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'co.epitre.aelf_lectures'
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.1'

    // Material design
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'com.google.android.material:material:1.9.0'

    // HTTP layer
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.squareup.moshi:moshi:1.15.0'

    // Foundation
    implementation 'org.greenrobot:eventbus:3.3.1'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Sqlite
    implementation project(path: ':sqlite3')
}

tasks.register('downloadMarkJs', DownloadFile) {
    sourceUrl = 'https://raw.githubusercontent.com/julmot/mark.js/8.11.1/dist/mark.min.js'
    target = new File('./src/main/assets/js/mark.8.11.1.min.js')
}
preBuild.dependsOn downloadMarkJs

//
// Custom tasks
//

class DownloadFile extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}
